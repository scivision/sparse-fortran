# Finds Lapack, tests, and if not found or broken, autobuild Lapack
if(lapack_external)
  include(${CMAKE_CURRENT_LIST_DIR}/lapack_external.cmake)
  return()
endif()

if(autobuild)
  find_package(LAPACK)
else()
  find_package(LAPACK REQUIRED)
endif()

if(LAPACK_FOUND)
  set(lapack_external false CACHE BOOL "autobuild Lapack")
else()
  include(${CMAKE_CURRENT_LIST_DIR}/lapack_external.cmake)
  return()
endif()

# -- verify links

set(CMAKE_REQUIRED_FLAGS)
set(CMAKE_REQUIRED_LINK_OPTIONS)
set(CMAKE_REQUIRED_INCLUDES)
set(CMAKE_REQUIRED_LIBRARIES LAPACK::LAPACK)
include(CheckFortranSourceCompiles)

if("d" IN_LIST arith)
  check_fortran_source_compiles("double precision, external :: disnan; print *, disnan(0.); end" LAPACK_real64_link SRC_EXT f90)
  if(NOT LAPACK_real64_link)
    message(STATUS "Lapack ${LAPACK_LIBRARIES} not building with ${CMAKE_Fortran_COMPILER_ID} ${CMAKE_Fortran_COMPILER_VERSION}")
    if(NOT autobuild)
      message(FATAL_ERROR "autobuild=off, so cannot proceed")
    endif()
    include(${CMAKE_CURRENT_LIST_DIR}/lapack_external.cmake)
    return()
  endif()
endif()

if("s" IN_LIST arith)
  check_fortran_source_compiles("real, external :: sisnan; print *, sisnan(0.); end" LAPACK_real32_link SRC_EXT f90)
  if(NOT LAPACK_real32_link)
    message(STATUS "Lapack ${LAPACK_LIBRARIES} not building with ${CMAKE_Fortran_COMPILER_ID} ${CMAKE_Fortran_COMPILER_VERSION}")
    if(NOT autobuild)
      message(FATAL_ERROR "autobuild=off, so cannot proceed")
    endif()
    include(${CMAKE_CURRENT_LIST_DIR}/lapack_external.cmake)
    return()
  endif()
endif()
